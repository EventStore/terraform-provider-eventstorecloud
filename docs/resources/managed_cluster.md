---
page_title: "Resource eventstorecloud_managed_cluster - terraform-provider-eventstorecloud"
subcategory: ""
description: |-
  Manages EventStoreDB instances and clusters in Event Store Cloud
---

# Resource (eventstorecloud_managed_cluster)

Manages EventStoreDB instances and clusters in Event Store Cloud

## Example Usage

```terraform
# Example for AWS

data "eventstorecloud_project" "example" {
  name = "Example Project"
}

resource "eventstorecloud_network" "example" {
  name = "Example Network"

  project_id = eventstorecloud_project.example.id

  resource_provider = "aws"
  region            = "us-west-2"
  cidr_block        = "172.21.0.0/16"
}

resource "eventstorecloud_managed_cluster" "example" {
  name = "Example Cluster"

  project_id = eventstorecloud_network.example.project_id
  network_id = eventstorecloud_network.example.id

  topology        = "three-node-multi-zone"
  instance_type   = "F1"
  disk_size       = 24
  disk_type       = "gp3"
  disk_iops       = 3000
  disk_throughput = 125
  server_version  = "23.10"
}
```

## Accessing Initial Credentials

When a managed cluster is created, EventStore Cloud generates initial credentials for the `admin` and `ops` users. These credentials are available as computed attributes and can be accessed via Terraform outputs.

### Example: Outputting Credentials

```terraform
resource "eventstorecloud_managed_cluster" "example" {
  # ... cluster configuration
}

# Output the admin password
output "cluster_admin_password" {
  description = "Initial admin password for the EventStore cluster"
  value       = eventstorecloud_managed_cluster.example.initial_admin_password
  sensitive   = true
}

# Output the ops password
output "cluster_ops_password" {
  description = "Initial ops password for the EventStore cluster"
  value       = eventstorecloud_managed_cluster.example.initial_ops_password
  sensitive   = true
}

# Output the credentials generation timestamp
output "credentials_generated_at" {
  description = "Timestamp when the initial credentials were generated"
  value       = eventstorecloud_managed_cluster.example.credentials_generated_at
}
```

### Accessing the Outputs

After running `terraform apply`, you can retrieve the credentials:

```bash
# View the credentials generation timestamp
terraform output credentials_generated_at

# View sensitive credentials (requires -raw flag)
terraform output -raw cluster_admin_password
terraform output -raw cluster_ops_password
```

### Important Notes

- **Initial credentials are only available once** and may be cleared by EventStore Cloud for security reasons
- **Credentials are marked as sensitive** to prevent accidental exposure in logs
- **If credentials are no longer available**, the fields will be empty but the cluster will continue to function normally
- **Use these credentials promptly** to create additional users or change passwords as needed

<!-- schema generated by tfplugindocs -->

## Schema

### Required

- **disk_size** (Number) Size of the data disks, in gigabytes
- **disk_type** (String) Storage class of the data disks (find the list of valid values below)
- **instance_type** (String) Instance type of the managed cluster (find the list of valid values below). A different instance type will trigger a resize operation.
- **name** (String) Name of the managed cluster
- **network_id** (String) ID of the network in which the managed cluster exists
- **project_id** (String) ID of the project in which the managed cluster exists
- **server_version** (String) Server version to provision (find the list of valid values below)
- **topology** (String) Topology of the managed cluster (`single-node` or `three-node-multi-zone`)

### Optional

- **disk_iops** (Number) Number of IOPS for storage, required if disk_type is `gp3`
- **disk_throughput** (Number) Throughput in MB/s for storage, required if disk_type is `gp3`
- **id** (String) The ID of this resource.
- **projection_level** (String) Determines whether to run no projections, system projections only, or system and user projections (find the list of valid values below) Defaults to `off`.
- **protected** (Boolean) Protection from an accidental cluster deletion Defaults to `false`.
- **server_version_tag** (String) Server version tag to provision (find the list of valid values below). A higher server_version_tag will prompt an upgrade.

### Read-Only

- **credentials_generated_at** (String) Timestamp when the initial credentials were generated
- **dns_name** (String) DNS address of the cluster
- **initial_admin_password** (String, Sensitive) Initial password for the admin user
- **initial_ops_password** (String, Sensitive) Initial password for the ops user
- **region** (String) Region in which the cluster was created. Determined by the region of the Network
- **resource_provider** (String) Provider in which the cluster was created. Determined by the provider of the Network.

### Valid Values

Use only the following values as `disk_type`:

| Provider     | Disk types        |
| :----------- | :---------------- |
| Azure        | `premium-ssd-lrs` |
| Google Cloud | `ssd`             |
| AWS          | `gp2`, `gp3`      |

For `server_version`, use one of the available EventStoreDB product versions:

- `23.10`
- `24.10`
- `25.0`

For `projection_level`, use one of the following values:

- `off` (the projections subsystem is disabled completely)
- `system` (only run system projections)
- `user` (both system and custom JavaScript projections are enabled)

Use one of the available instance types as `instance_type`:

Small-scale:

- `F1`
- `C4`

Production scale:

- `M8`
- `M16`
- `M32`
- `M64`
- `M128`

Check our [instance size guide](https://developers.eventstore.com/cloud/provision/cloud-instance-guidance/) to choose the right size for your workload.

Use one of the following values as `topology`:

- `single-node`
- `three-node-multi-zone`

## Import

Import is supported using the following syntax:

```shell
terraform import eventstorecloud_managed_cluster.example project_id:cluster_id
```
